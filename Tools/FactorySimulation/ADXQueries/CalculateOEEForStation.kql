.create-or-alter function CalculateOEEForStation(stationName:string,location:string,idealCycleTime:int,hoursInShift:int) {
// calculation from https://www.oee.com/calculating-oee/
let dataHistoryTable = AdtPropertyEvents;
let idealRunningTime = hoursInShift * 60 * 60; // [seconds]
let shiftEndTime = now();
let shiftStartTime = datetime_add('hour', -hoursInShift, shiftEndTime);
let numProdTable = dataHistoryTable
| where TimeStamp > shiftStartTime and TimeStamp < shiftEndTime
| where Id == toscalar(GetDigitalTwinIdForUANode(stationName, location, "NumberOfManufacturedProducts"));
let numScrapTable = dataHistoryTable
| where TimeStamp > shiftStartTime and TimeStamp < shiftEndTime
| where Id == toscalar(GetDigitalTwinIdForUANode(stationName, location, "NumberOfDiscardedProducts"));
let cycleTimeTable = dataHistoryTable
| where TimeStamp > shiftStartTime and TimeStamp < shiftEndTime
| where Id == toscalar(GetDigitalTwinIdForUANode(stationName, location, "ActualCycleTime"));
let faultyTimeTable = dataHistoryTable
| where TimeStamp > shiftStartTime and TimeStamp < shiftEndTime
| where Id == toscalar(GetDigitalTwinIdForUANode(stationName, location, "FaultyTime"));
let numProdEnd = toscalar(
numProdTable
| where isnotnull(SourceTimeStamp)
| extend numProd = toint(Value)
| summarize max(numProd));
let numProdStart = toscalar(
numProdTable
| where isnotnull(SourceTimeStamp)
| extend numProd = toint(Value)
| summarize min(numProd));
let numScrapEnd = toscalar(
numScrapTable
| where isnotnull(SourceTimeStamp)
| extend numDisc = toint(Value)
| summarize max(numDisc));
let numScrapStart = toscalar(
numScrapTable
| where isnotnull(SourceTimeStamp)
| extend numDisc = toint(Value)
| summarize min(numDisc));
let numProdShift = numProdEnd - numProdStart;
let numScrapShift = numScrapEnd - numScrapStart;
let actualCycleTime = toscalar(
cycleTimeTable
| where isnotnull(SourceTimeStamp)
| extend cycleTime = toint(Value)
| summarize avg(cycleTime));
let faultyTimeShift = toscalar(
faultyTimeTable
| where isnotnull(SourceTimeStamp)
| extend faultyTime = toint(Value)
| summarize sum(faultyTime));
let availability = todouble(idealRunningTime - faultyTimeShift) / todouble(idealRunningTime);
let performance = todouble(idealCycleTime) * todouble(numProdShift + numScrapShift) / todouble(idealRunningTime - faultyTimeShift);
let quality = todouble(numProdShift) / todouble(numProdShift + numScrapShift);
let oee = availability * performance * quality;
toscalar(oee)
}
